(require lispy.macros *)

(import json)
(from collections [Counter defaultdict])
(from math [isqrt])
(from time [time])

(def counter-to-str [d]
  (return (-> d
              (.items)
              (sorted)
              (->> (map (fn [x] (+ (sub x 0)
                                   (str (sub x 1)))))
                   (.join ",")))))

(def get-all-anagrams [lst]
  (= mem (defaultdict list))
  (for word lst
    (.append (sub mem (counter-to-str (Counter word))) word))
  (= rst [])
  (for [k v] (.items mem)
    (when (>= (len v) 2)
      (rst.append v)))
  (return rst))

(def square-p [n]
  (return (== n (** (isqrt n) 2))))

(def find-square-pairs [anagrams]
  ;; digit candidates for each chars
  (= candidates {})
  (for d (sub anagrams 0)
    (= (sub candidates d) (set (range 10))))
  ;; no leading zeroes
  (for anagram anagrams
    (.discard (sub candidates (sub anagram 0)) 0))
  
  (def recur [candidates :translation {} :rst (list)]
    (when (not candidates)
      (= nums (->> anagrams
                   (map (fn [w] (map (fn [c] (str (sub translation c))) (list w))))
                   (map (fn [x] (->> x (.join "") (int))))
                   (filter square-p)
                   (list)))
      (when (>= (len nums) 2)
        (rst.append nums))
      (return rst))
    (= [c ds] (candidates.popitem))
    (for d ds
      (when (not (in d (.values translation)))
        (recur candidates (| translation {c d}))))
    (= (sub candidates c) ds)
    (return rst))
  (return (recur candidates)))

(def solution []
  ;; Load words list
  (with [(open "./p098_words.txt" "r") as f]
    (= s (f.read)))
  (= word-list (json.loads (+ "[" s "]")))

  (= res 0)
  (for anagrams (get-all-anagrams word-list)
    (= square-pairs (find-square-pairs anagrams))
    (when square-pairs
      (= res (->> square-pairs
                  (map max)
                  (max)
                  (max res)))))
  (return res))

(def main []
  (= start (time))
  (print (solution))
  (print "elapsed:" (- (time) start)))

(when (== __name__ "__main__")
  (main))
