(require lispy.macros *)

(def lst-to-num [lst]
  (= ret 0)
  (for d lst
    (*= ret 10)
    (+= ret d))
  (return ret))

(def num-to-set [n]
  (= ret (set))
  (while n
    (ret.add (% n 10))
    (//= n 10))
  (return ret))

(defmacro with-permutations [perm candidates lengths *body]
  (return `(do
     (= __candidates ~candidates)
     (= __lengths (set ~lengths))
     (= __max-depth (max ~lengths))
     (= __terminate [])
     (= ~perm [])
     (def __recur [__l __d]
       (if (not __terminate)
           (do
             (if (in __l __lengths)
                 (do ~@body))
             (if __d
                 (for __c (list __candidates)
                   (.append ~perm __c)
                   (.remove __candidates __c)
                   (__recur (+ __l 1) (- __d 1))
                   (.add __candidates __c)
                   (.pop ~perm))))))
     (__recur 0 __max-depth))))

(def propagate [p]
  (= ret (str p))
  (= i 2)
  (while (< (len ret) 9)
    (+= ret (str (* p i)))
    (+= i 1))
  (return (int ret)))

(def pandigital? [n :l 9]
  (return (and (== (len (str n)) l)
               (== (num-to-set n) (set (range 1 (+ 1 l)))))))

(def sol []
  (= candidates (set (range 1 10)))
  (= ret 0)
  (with-permutations perm candidates (range 1 5)
    (= p (lst-to-num perm))
    (= prop (propagate p))
    (if (pandigital? prop)
        (do (nonlocal ret)
            (= ret (max ret prop)))))
  (return ret))

(print (sol))
